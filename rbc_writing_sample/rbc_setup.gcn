
#Reducing dimensionality by substituting out variables that are redundant
tryreduce
{
  K_d[], L_d[], lambda_c[], pi[], PI[];
};


#Setting up representative household side of optimization problem
block CONSUMER
{
  definitions
  {
    u[] = (C[]^mu * (1 - L_s[])^(1 - mu))^(1 - eta) / (1 - eta);
  };
  controls
  {
    K_s[], C[], L_s[], I[];
  };
  objective
  {
    U[] = u[] + beta * E[][U[1]];
  };
  constraints
  {
    I[] + C[] = r[] * K_s[-1] + W[] * L_s[] + pi[] : lambda_c[];
    
    K_s[] = (1 - delta) * K_s[-1] + I[];
  };
  calibration
  {
    delta = 0.025;
    beta = 0.99;
    eta = 1.5;
    mu = 0.3;
  };
};


#Setting up representative firm side of optimization problem
block FIRM
{
  controls
  {
    K_d[], L_d[], Y[], pi[];
  };
  objective
  {
    PI[] = pi[];
  };
  constraints
  {
    Y[] = A[] * K_d[]^alpha * L_d[]^(1 - alpha);
    pi[] = Y[] - L_d[] * W[] - r[] * K_d[];
  };
  calibration
  {
    alpha = 0.36;
  };
};


#Setting equilibrium market clearing conditions
block EQUILIBRIUM
{
  identities
  {
    K_d[] = K_s[-1];
    L_d[] = L_s[];
  };
};


#Introducing stochastic exogenous shock mechanism
block EXOG
{
  identities
  {
    A[] = exp(rho * log(A[-1]) + epsilon_A[]);
  };
  shocks
  {
    epsilon_A[];
  };
  calibration
  {
    rho = 0.979;
  };
};
